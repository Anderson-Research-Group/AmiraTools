# Amira-Script-Object 620
# L. Schuring 1/2025
# TO-DO: figure out testBreak. Need to delete hidden modules if user interrupts the script

#########################################################################
# CONSTRUCTOR: This method sets up the script module in Amira
#########################################################################
$this proc constructor {} {
	$this data setLabel "Original"
	set dataDicom [$this data] 
	set dataTif [$this newPortConnection Seg3D-export HxUniformScalarField3] 
	$this newPortDoIt "ApplyButton"	
	$this ApplyButton setVisibility 0
	#$this testBreak breakButton
  }
  

#########################################################################
# CLEANUP: 
# This procedure removes any intermediate modules that were generated by the script
#########################################################################

proc cleanUp {beforeMods} {
	echo $beforeMods
	set afterMods [all -hidden]
	echo $afterMods

	# Initialize an empty list for removal of intermediate modules
	set delMods ""

	# Iterate over each element in afterMods, and compare to beforeMods to determine if it needs to be deleted
	foreach item $afterMods {
	    # Check if the item is not in startList
	    if {[lsearch -exact $beforeMods $item] == -1} {
	        # Append the item to delMod
	        lappend delMods $item
	    }
	}
	
	# remove intermediate mods
	foreach item $delMods {
		remove $item
	}

}

#########################################################################
# DESTRUCTOR: Place-holder for now. Useful for clean-up
#########################################################################
$this proc destructor { } {
	echo "Removing Script Object"
}
#########################################################################
# COMPUTE: 
# Once both datasets (dicom and tif) are selected, "Apply" button will appear
# If Apply button is hit, "AL_AutoAnkleLabels.hx" will run 
#########################################################################
$this proc compute {} {
	if {[$this data getState] != "disconnect" & [$this Seg3D-export getState] != "disconnect"} {
		# if both original and Seg3D-export data is available, make apply buttom visible
		$this ApplyButton setVisibility 1
	}
	
	if {[$this ApplyButton wasHit 0] } {
		# set variables to be used by AutoAnkleLabels.hx
		set dataDicom [$this data source]
		set dataTif [$this Seg3D-export source]

		# identify current hidden modules in Amira so that intermediate modules can be deleted after running script
		set beforeMods [all -hidden]
		global hideNewModules
		set hideNewModules 1

		# Define script location relative to global variable, AMIRA_LOCAL
		global AMIRA_LOCAL
		source "$AMIRA_LOCAL/share/script-objects/AL_AutoAnkleLabels.tcl"

		# After script runs, run clean-up procedure to remove hidden intermediate modules
		set hideNewModules 0
		cleanUp $beforeMods
	}
}


